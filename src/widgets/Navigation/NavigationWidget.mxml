<?xml version="1.0" encoding="utf-8"?>
<!--
///////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010-2011 Esri. All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////
-->
<viewer:BaseWidget xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:viewer="com.esri.viewer.*"
                   xmlns:Navigation="widgets.Navigation.*"
                   initialize="basewidget_initializeHandler(event)"
                   widgetConfigLoaded="init()">
    <fx:Declarations>
        <s:Fade id="rollOverFade" target="{navigation}"/>
        <s:Fade id="rollOutFade" target="{navigation}"/>
    </fx:Declarations>
    <fx:Script>
        <![CDATA[
			import com.esri.ags.events.ZoomEvent;
			import com.esri.ags.geometry.Extent;
			import com.esri.viewer.AppEvent;
			
			import mx.controls.Alert;
			import mx.events.FlexEvent;

            private var buttonBarLastSelectedIndex:int;

            protected function basewidget_initializeHandler(event:FlexEvent):void
            {
                AppEvent.addListener(AppEvent.DATA_PUBLISH, sharedDataUpdated);
            }

            private function sharedDataUpdated(event:AppEvent):void
            {
                var data:Object = event.data;

                if (data.key == "Edit_Widget") // disable buttonbar when edit widget is opened
                {
                    if (data.collection[0])
                    {
                        map.cursorManager.removeAllCursors();
                        buttonBarLastSelectedIndex = navigation.btnBar.selectedIndex;
                        navigation.btnBar.selectedIndex = 0;
                        navigation.btnBar.enabled = false;
                    }
                    else
                    {
                        navigation.btnBar.selectedIndex = buttonBarLastSelectedIndex;
                        navigation.btnBar.enabled = true;
                    }
                }
            }

			private var _minlevel:int = -1;
			private var _maxlevel:int = -1;
			
            private function init():void
            {
                if (configXML)
                {
                    var rollOverAlpha:Number = configXML.rolloveralpha[0] ? parseFloat(configXML.rolloveralpha) : 1;
                    var rollOutAlpha:Number = configXML.rolloutalpha[0] ? parseFloat(configXML.rolloutalpha) : 0.69;

                    rollOutFade.alphaTo = rollOutAlpha;
                    rollOverFade.alphaTo = rollOverAlpha;
                    navigation.alpha = rollOutAlpha;
                    navigation.visible = true;

                    navigation.panwheelItem = new NavToolItem("PanWheel", "", configXML.panwheel.@visible == 'true');
                    navigation.zoomFullextItem = new NavToolItem(configXML.panwheel.@fullextlabel || getDefaultString("fullExtentLabel"), configXML.panwheel.@fullexticon, true);
                    navigation.pandownItem = new NavToolItem(configXML.panwheel.@pandownlabel || getDefaultString("panDownLabel"), "", true);
                    navigation.panleftItem = new NavToolItem(configXML.panwheel.@panleftlabel || getDefaultString("panLeftLabel"), "", true);
                    navigation.panrightItem = new NavToolItem(configXML.panwheel.@panrightlabel || getDefaultString("panRightLabel"), "", true);
                    navigation.panupItem = new NavToolItem(configXML.panwheel.@panuplabel || getDefaultString("panUpLabel"), "", true);
                    navigation.prevextItem = new NavToolItem(configXML.prevextbutton.@label || getDefaultString("previousExtentLabel"), "", configXML.prevextbutton.@visible == 'true');
                    navigation.nextextItem = new NavToolItem(configXML.nextextbutton.@label || getDefaultString("nextExtentLabel"), "", configXML.nextextbutton.@visible == 'true');
                    navigation.panItem = new NavToolItem(configXML.panbutton.@label || getDefaultString("panLabel"), configXML.panbutton.@icon, configXML.panbutton.@visible == 'true');
                    navigation.zoominItem = new NavToolItem(configXML.zoominbutton.@label || getDefaultString("zoomInLabel"), configXML.zoominbutton.@icon, configXML.zoominbutton.@visible == 'true');
                    navigation.zoomoutItem = new NavToolItem(configXML.zoomoutbutton.@label || getDefaultString("zoomOutLabel"), configXML.zoomoutbutton.@icon, configXML.zoomoutbutton.@visible == 'true');
                    navigation.initButtonBar();
					
					_minlevel = int(configXML.minlevel);
					_maxlevel = int(configXML.maxlevel);
					
					if(_minlevel != 0 && _maxlevel != 0 )
					{
						map.addEventListener(ZoomEvent.ZOOM_START,map_zoomstartHandler);
						map.addEventListener(ZoomEvent.ZOOM_END,map_zoomendHandler);
					}
					
					AppEvent.addListener(AppEvent.POST_PROCESS,postProcess_handler);
                }
            }
			
			private var _preExtent:Extent;
			private var _changeExtent:Boolean = false;
			
			private function map_zoomstartHandler(event:ZoomEvent):void
			{
				if(event.level<_minlevel)
				{
					event.stopImmediatePropagation();
					_changeExtent = true;
					Alert.show("这是最小显示级别");
					_preExtent = map.extent;
				}
				else if(event.level>_maxlevel)
				{
					_changeExtent = true;
					Alert.show("这是最大显示级别");
					_preExtent = map.extent;
				}
			}
			
			private function map_zoomendHandler(event:ZoomEvent):void
			{
				if(_changeExtent)
				{
					map.extent = _preExtent;
					_changeExtent = false;
				}
			}
			
			private function postProcess_handler(event:AppEvent):void
			{
				//判断当前是否处于地图对比场景，如果是的话，隐藏自己
				if(event.data != null && event.data.type == "AttachedMapStateChange")
				{
					if(event.data.state == "open")
					{
						navigation.visible = false;
					}
					else
					{
						navigation.visible = true;
					}
				}
			}
        ]]>
    </fx:Script>
    <Navigation:Navigation id="navigation"
                           layoutDirection="ltr"
                           map="{map}"
						   rollOut="{rollOutFade.play()}"
						   rollOver="{rollOverFade.play()}"
                           visible="false"/>
</viewer:BaseWidget>
